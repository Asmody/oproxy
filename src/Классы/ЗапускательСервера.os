&Пластилин Перем ОбработчикСоединений;
Перем ПроверкиПроксиСервера;
Перем АдресСервера Экспорт;
Перем ПортСервера Экспорт;
Перем ПортПрокси Экспорт;

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры 

Процедура ПодключитьПроверки()
	ИскомыйТип = "ПроверкиПроксиСервера";
	ИскомыйФайл = СтрШаблон("%1.os", ИскомыйТип);
	ТекущийКаталог = ТекущийКаталог();
	Файлы = НайтиФайлы(ТекущийКаталог, ИскомыйФайл);
	Если ЗначениеЗаполнено(Файлы) Тогда
		Файл = Файлы[0];
		ПодключитьСценарий(Файл.ПолноеИмя, Файл.ИмяБезРасширения);
	Иначе
		ТекстОшибки = СтрШаблон("
		|Не найден файл %1 в %2
		|Выполните команду oproxy init
		|", ИскомыйФайл, ТекущийКаталог);
		Сообщить(ТекстОшибки);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	РефлекторОбъекта = Новый Рефлектор;
	МассивИменНужныхМетодов = Новый Массив;
	МассивИменНужныхМетодов.Добавить("ОбработкаПомещенияВХранилище");
	МассивИменНужныхМетодов.Добавить("ОбработкаИзмененияВерсииХранилища");
	Для каждого ИмяНужногоМетода из МассивИменНужныхМетодов Цикл
		Если НЕ РефлекторОбъекта.МетодСуществует(Тип(ИскомыйТип), ИмяНужногоМетода) Тогда
			ТекстОшибки = СтрШаблон("Не найден метод %1 в файле %2", ИмяНужногоМетода, ОбъединитьПути(ТекущийКаталог, ИскомыйФайл));
			Сообщить(ТекстОшибки);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	КонецЦикла;
	ПроверкиПроксиСервера = Новый ПроверкиПроксиСервера;
КонецПроцедуры

Процедура Запустить() Экспорт
	ПодключитьПроверки();
	Попытка
		Сервер = Новый TCPСервер(ПортПрокси);
		Сервер.Запустить();
		Шаблон = "Прокси-сервер запущен на порту %1 к хранилищу tcp://%2:%3";
		Сообщить(СтрШаблон(Шаблон, ПортПрокси, АдресСервера, ПортСервера));
	Исключение
		Шаблон =
		"
		|###################################################################################
		| >>> Ошибка запуска прокси-сервера на порту %1 к хранилищу tcp://%2:%3
		| >>> Возможно, порт %1 занят другой программой (или запущенным экземпляром этой)
		|###################################################################################
		|
		|%4";
		Сообщить(СтрШаблон(Шаблон, ПортПрокси, АдресСервера, ПортСервера, ОписаниеОшибки()));
	КонецПопытки;
	ОбработчикСоединений.ПроверкиПроксиСервера = ПроверкиПроксиСервера;
	ОбработчикСоединений.АдресСервера = АдресСервера;
	ОбработчикСоединений.ПортСервера = ПортСервера;
	Пока Истина Цикл
		Соединение = Сервер.ОжидатьСоединения();
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Соединение);
		ФоновыеЗадания.Выполнить(ОбработчикСоединений, "ОбработатьСоединение", МассивПараметров);
	КонецЦикла;
КонецПроцедуры
